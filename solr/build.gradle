description = 'Solr Service'

apply plugin: 'java'
apply plugin: 'jetty'

repositories { mavenCentral() }

configurations {
    solrApp
    solrDep
}

dependencies {
    solrApp 'org.apache.solr:solr:4.9.0@war'

    runtime group: 'org.slf4j', name: 'slf4j-api', version: '1.7.+'
    runtime group: 'log4j', name: 'log4j', version: '1.2.+'
}

task copySolrConfig(type: Copy) {
    from "$projectDir/config"
    into "$buildDir/config"

    // prevent up-to-date state
    outputs.upToDateWhen { false }
}

task unzipSolr(type: Copy) {
    from zipTree(configurations.solrApp.singleFile)
    into "$buildDir/webapp"
}

task run(type: JettyRun) {
    dependsOn copySolrConfig, \
              unzipSolr, \
              jettyStop

    webAppSourceDirectory = file("$buildDir/webapp")
    contextPath = 'solr'
    httpPort = 8983
    daemon = true

    doFirst { System.setProperty('solr.solr.home', "$buildDir/config") }
}

task stop() { dependsOn jettyStop }

[ run, jettyStop ]*.stopPort = 8984
[ run, jettyStop ]*.stopKey = 'stopKey'